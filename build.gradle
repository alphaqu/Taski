plugins {
    id 'java'

    // Publishing
    id 'maven-publish'
    id 'signing'
}

group 'dev.notalpha'
version '2.1.0'

java {
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}


compileJava {
    options.release = 8
}

dependencies {
    implementation 'org.jetbrains:annotations:23.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

// Publishing
publishing {
    repositories {
        maven {
            name = "notalpha"
            url = "https://notalpha.dev/maven/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = project.name
                description = 'Task Management Library.'
                url = 'https://github.com/alphaqu/Taski'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/alphaqu/Taski.git'
                    developerConnection = 'scm:git:ssh://github.com:alphaqu/Taski.git'
                    url = 'http://github.com/alphaqu/Taski/tree/master'
                }
                developers {
                    developer {
                        id = 'notalpha'
                        name = 'Yan Chubikov'
                        email = 'yan.gyunashyan@gmail.com'
                    }
                }
            }
        }
    }
}

artifacts {
    archives javadocJar, sourcesJar
}
